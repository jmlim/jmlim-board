plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    //query dsl
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    // asciidoctor (spring rest docs)
    id "org.asciidoctor.convert" version "1.5.9.2"  // (1)
    id 'java'
}

group = 'io.jmlim.springboot.sample'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}

sourceSets {
    main.java.srcDir querydslDir
}

configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
//querydsl 추가 끝

// rest docs ====i
ext {
    snippetsDir = file('build/generated-snippets')          // (2)
}
ext['spring-restdocs.version'] = '2.0.4.RELEASE'   // (3)

test {
    useJUnitPlatform()
    outputs.dir snippetsDir // (4)
}

asciidoctor {
   // sourceDir 'src/docs/asciidoc'
    inputs.dir snippetsDir  // (5)
    dependsOn test          // (6)
}

bootJar {
    dependsOn asciidoctor                      // (7)
    from ("${asciidoctor.outputDir}/html5") { // (8)
        into 'static/docs'
    }
}
// rest docs end ====

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.querydsl:querydsl-jpa'

    // rest docs start
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor' // (9)
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc' // (10)
    // rest docs end

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'


    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}